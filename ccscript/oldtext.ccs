import "tweaks/EBPP_Disassembly_HardwareCompatible_v1_1.ccs"
import "tweaks/EbppMoreStuff.ccs"
import "tweaks/asm_run.ccs"
import "tweaks/battle_text_abridgements.ccs"
import "tweaks/bite_sized_improvements.ccs"
import "tweaks/deathtext.ccs"
import "tweaks/debugmenu.ccs"
import "tweaks/file_menu_rewrite.ccs"
import "tweaks/InvertSwirl1.ccs"
import "tweaks/custom_windows_standalone.ccs"
import "tweaks/save_window_text_positions.ccs"

newgame_location(7264,1898)
newgame_startup(init)

ROM[0xEF12C2] = byte 0 //disable tileset animations for Summers, which I use for the UFO tileset

init:
  music_stop
  customFlags.initialize
  helpers.equip_char_with_item(1, 2) //Equip main character with the Camera item
  givemoney(40)
  hotspot_on(1,43,hotspotTest)
  save
  music_resume
  eob

hotspotTest:
  window_open(1)
  call(JeffJoinText)
  window_closeall
  eob

JeffJoinText:
  window_closeall
  show_sprite_float(1279, FLOAT_EXCLAIM) //! bubble
  pause(60)
  hide_sprite_float(1279)
  pause(10)
  window_open(1)
  "@Hey, {name(1)}!" next
  "@You seemed really excited on the phone about those photographs." next
  "@Let's go to the chemistry lab and develop them!" next
  "@It's the weekend, so I've got plenty of time to spare." wait

  helpers.sprite2_do_mc(3, 0x2F)
  window_closeall
  pause(0x60)
  hide_sprite(0x4FF, 6) //hide the Jeff NPC sprite
  sound(0x16)
  music(0xB)
  pause(120) pause(120) pause(120) pause(60)
  music_resume
  party_add(3)
  window_closeall
  unset(customFlags.jeffVisible)
  set(customFlags.jeffInParty)
  eob

txtChemicals:
  if not hasitem(1, 143)
    goto(helptext.filmHelpText)
  else
    goto(txtFilmUse)
  eob

txtFilmGet:
  if not customFlags.darkroom
  {
    //TODO: Palette swap the room like in Magicant
    take(1, 140) //remove the canister from his inventory
    give(1, 143) //give him the film
    //Tint the screen
    "@You removed the film from the canister and started using the chemicals." next
    "@You got the {itemname(143)}!{sound(102)}" wait
    
    set(customFlags.darkroom) //Use this to keep the player from ruining the exposed film!!
    eob
  }
  else goto(txtFilmUse)

txtFilmUse:
  //"@It's the film!" next
  if customFlags.photo3_viewed
  {
    "  (Display this string when all of the photos are developed.)" wait
  }

  if not customFlags.photo1_viewed
  {
    set(customFlags.photo1_viewed)
    "@You dipped the film in the chemicals." next
    "@Well, let's see here..." next
    "@It's a picture of the bell tower early in the morning." next //https://photos.app.goo.gl/3gmnwdSqYyp1oMgg7 TODO: arrange these and include them in the ZIP after the game is done
    call(portfolioText) end
  }
  else goto(photo2_text)
  eob

photo2_text:
    if not customFlags.photo2_viewed
    {
      set(customFlags.photo2_viewed)
      "@It's a photo you took of your friends waiting for the bus." next //https://photos.app.goo.gl/fMqyBm2AoeQZdFTE6
      call(portfolioText) end
    }
    else goto(photo3_text)
    eob

photo3_text:
    if not customFlags.photo3_viewed
    {
      set(customFlags.photo3_viewed)
      unset(customFlags.darkroom)
      "@It's a photo taken at the top of Pavilion Hill at sunset." next //https://photos.app.goo.gl/AZ1eA8NmEEtmJdnk7
      "@There's a boy and a girl walking downhill, holding hands." next
      "@The girl is holding a skateboard." next
      "@Wait a second..." next
  
      helpers.char_look_at(1, 3) //Turn Ness to face Jeff
      window_clear
      "@Hey, let me take a closer look at that shot. " next
      "@Is that...a UFO above those trees!?" next
      helpers.char_look_at(1, 594) //Turn Ness to face Maxwell
      helpers.char_look_at(3, 594) //Turn Jeff to face Maxwell
      sprite_direction(594, 3) //Actually turn Maxwell's sprite
      window_clear
      "@Whoa, that looks amazing!" next
      music_effect(3)
      "@I overheard some other students talking about flying saucers earlier." next
      "@Say, I know it's against curfew..." next
      "@But you two should try to get more pictures!" next
      set(flag 88) //Enables leaving Snow Wood? What does this do? Official flag name is "FLG_ITEM_GAUS"
      "@You'd probably get in the paper again!" wait
  
      //CUTSCENE TIME!
      window_closeall
      unset(flag 335) //disable Tony crouching sprite
      unset(customFlags.disable_enemies)
      helpers.fade_out
      pause(120)
      helpers.later_that_night
      warp(1) //warp to the outside of Snow Wood. This also fades out the "Later that night..." text
      "[1F E5 FF]" //freeze character FF's movement. What is this doing here? Can it be removed?
      pause(1)
      window_open(1)
      take(1, 143) //get rid of the film
      take(3, 143) //ditto, just in case the Player gave it to Jeff
      save
      "@All right, let's head to Lake Tess!" next
      "@I've got all of your equipment, and plenty of extra rolls of film." next
      "@By the way, if you need to run back for any reason," next
      "  like, if you forgot something," next
      "  don't forget to hold down the Y Button!" wait //that's right, this game has the run button patch!
    }
    eob

portfolioText:
  "@You put it in your portfolio." eob
